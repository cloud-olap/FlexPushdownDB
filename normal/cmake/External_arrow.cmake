# Arrow
set(ARROW_VERSION "apache-arrow-0.16.0")
set(ARROW_GIT_URL "https://github.com/apache/arrow.git")


include(ExternalProject)
find_package(Git REQUIRED)


set(ARROW_BASE arrow_ep)
set(ARROW_PREFIX ${DEPS_PREFIX}/${ARROW_BASE})
set(ARROW_BASE_DIR ${CMAKE_CURRENT_BINARY_DIR}/${ARROW_PREFIX})
set(ARROW_INSTALL_DIR ${ARROW_BASE_DIR}/install)
set(ARROW_INCLUDE_DIR ${ARROW_INSTALL_DIR}/include)
set(ARROW_LIB_DIR ${ARROW_INSTALL_DIR}/lib)
set(ARROW_CORE_SHARED_LIBS ${ARROW_LIB_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}arrow${CMAKE_SHARED_LIBRARY_SUFFIX})
set(ARROW_CORE_STATIC_LIBS ${ARROW_LIB_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}arrow${CMAKE_STATIC_LIBRARY_SUFFIX})
set(ARROW_DATASET_SHARED_LIBS ${ARROW_LIB_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}arrow_dataset${CMAKE_SHARED_LIBRARY_SUFFIX})
set(ARROW_DATASET_STATIC_LIBS ${ARROW_LIB_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}arrow_dataset${CMAKE_STATIC_LIBRARY_SUFFIX})
set(ARROW_JEMALLOC_BASE_DIR ${ARROW_BASE_DIR}/src/${ARROW_BASE}-build/jemalloc_ep-prefix/src/jemalloc_ep/dist)
set(ARROW_JEMALLOC_SHARED_LIBS ${ARROW_JEMALLOC_BASE_DIR}/lib/${CMAKE_SHARED_LIBRARY_PREFIX}jemalloc${CMAKE_SHARED_LIBRARY_SUFFIX})
set(ARROW_JEMALLOC_STATIC_LIBS ${ARROW_JEMALLOC_BASE_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}jemalloc${CMAKE_STATIC_LIBRARY_SUFFIX})


ExternalProject_Add(${ARROW_BASE}
        PREFIX ${ARROW_PREFIX}
        GIT_REPOSITORY ${ARROW_GIT_URL}
        GIT_TAG ${ARROW_VERSION}
        SOURCE_SUBDIR cpp
        INSTALL_DIR ${ARROW_INSTALL_DIR}
        BUILD_BYPRODUCTS
        ${ARROW_CORE_SHARED_LIBS}
        ${ARROW_CORE_STATIC_LIBS}
        ${ARROW_DATASET_SHARED_LIBS}
        ${ARROW_DATASET_STATIC_LIBS}
        ${ARROW_JEMALLOC_SHARED_LIBS}
        ${ARROW_JEMALLOC_STATIC_LIBS}
        CMAKE_ARGS
        -DARROW_USE_CCACHE:BOOL=ON
        -DARROW_CSV:BOOL=ON
        -DARROW_DATASET:BOOL=ON
        -DARROW_FLIGHT:BOOL=OFF
        -DARROW_IPC:BOOL=OFF
        -DARROW_PARQUET:BOOL=OFF
        -DARROW_WITH_SNAPPY:BOOL=ON
        -DARROW_JEMALLOC:BOOL=ON
        -DCMAKE_INSTALL_MESSAGE=NEVER
        -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
        -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
        -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
        -DCMAKE_INSTALL_PREFIX=${ARROW_INSTALL_DIR}
        )


file(MAKE_DIRECTORY ${ARROW_INCLUDE_DIR}) # Include directory needs to exist to run configure step


add_library(arrow_static STATIC IMPORTED)
set_target_properties(arrow_static PROPERTIES IMPORTED_LOCATION ${ARROW_CORE_STATIC_LIBS})
set_target_properties(arrow_static PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${ARROW_INCLUDE_DIR})
set_target_properties(arrow_static PROPERTIES INTERFACE_LINK_LIBRARIES "${ARROW_JEMALLOC_STATIC_LIBS}")
add_dependencies(arrow_static ${ARROW_BASE})

add_library(arrow_shared STATIC IMPORTED)
set_target_properties(arrow_shared PROPERTIES IMPORTED_LOCATION ${ARROW_CORE_SHARED_LIBS})
set_target_properties(arrow_shared PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${ARROW_INCLUDE_DIR})
set_target_properties(arrow_shared PROPERTIES INTERFACE_LINK_LIBRARIES "${ARROW_JEMALLOC_SHARED_LIBS}")
add_dependencies(arrow_shared ${ARROW_BASE})

add_library(arrow_dataset_static STATIC IMPORTED)
set_target_properties(arrow_dataset_static PROPERTIES IMPORTED_LOCATION ${ARROW_DATASET_STATIC_LIBS})
set_target_properties(arrow_dataset_static PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${ARROW_INCLUDE_DIR})
add_dependencies(arrow_dataset_static ${ARROW_BASE})


#showTargetProps(arrow_static)
#showTargetProps(arrow_dataset_static)
